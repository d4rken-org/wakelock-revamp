buildscript {
    ext.buildConfig = [
            'compileSdk': 28,
            'minSdk'    : 16,
            'targetSdk' : 28,
            'buildtools': '28.0.3',

            'version'   : [
                    'major': 3,
                    'minor': 2,
                    'patch': 0,
                    'build': 0,
            ],
    ]

    ext.buildConfig.version['name'] = "${buildConfig.version.major}.${buildConfig.version.minor}.${buildConfig.version.patch}"
    ext.buildConfig.version['fullName'] = "${buildConfig.version.name}.${buildConfig.version.build}"
    ext.buildConfig.version['code'] = buildConfig.version.major * 1000000 + buildConfig.version.minor * 10000 + buildConfig.version.patch * 100 + buildConfig.version.build

    ext.versions = [
            'kotlin'             : '1.3.31',
            'kotlinCoroutines'   : '1.1.1',
            'kotlinSerialization': '0.10.0',
            'dagger'             : '2.22.1',
            'butterknife'        : '10.1.0',
            'rxjava'             : '2.2.6',
            'rxandroid'          : '2.1.0',
    ]

    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"
        classpath 'com.bugsnag:bugsnag-android-gradle-plugin:3.6.0'
    }
}

allprojects {
    repositories {
        google()
        jcenter()

        maven { url 'https://jitpack.io' }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
