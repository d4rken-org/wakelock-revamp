buildscript {
    ext.buildConfig = [
            'compileSdk': 28,
            'minSdk'    : 16,
            'targetSdk' : 28,
            'buildtools': '28.0.3',

            'version'   : [
                    'major': 3,
                    'minor': 1,
                    'patch': 5,
                    'build': 0,
            ],
    ]

    ext.buildConfig.version['name'] = "${buildConfig.version.major}.${buildConfig.version.minor}.${buildConfig.version.patch}"
    ext.buildConfig.version['fullName'] = "${buildConfig.version.name}.${buildConfig.version.build}"
    ext.buildConfig.version['code'] = buildConfig.version.major * 1000000 + buildConfig.version.minor * 10000 + buildConfig.version.patch * 100 + buildConfig.version.build

    ext.versions = [
            'supportLibrary'     : '28.0.0',
            'kotlin'             : '1.2.71',
            'kotlinCoroutines'   : '0.23.4',
            'kotlinSerialization': '0.6.0',
            'dagger'             : '2.16',
            'butterknife'        : '8.8.1',
            'rxjava'             : '2.1.16',
            'rxandroid'          : '2.0.2',
    ]

    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.3.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"
        classpath 'com.bugsnag:bugsnag-android-gradle-plugin:3.2.5'
    }
}

allprojects {
    repositories {
        google()
        jcenter()

        maven { url 'https://jitpack.io' }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
